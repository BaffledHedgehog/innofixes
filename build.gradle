plugins {
  id 'net.minecraftforge.gradle' version '6.0.+'
  id 'org.spongepowered.mixin' version '0.7.+'
}

group = project.mod_group
version = project.mod_version

base {
  archivesName = project.mod_id
}

java {
  toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
  mappings channel: 'official', version: project.minecraft_version

  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'debug'
      arg "-mixin.config=mixins.innofixes.json"
      mods {
        innofixes {
          source sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'debug'
      arg "-mixin.config=mixins.innofixes.json"
      mods {
        innofixes {
          source sourceSets.main
        }
      }
    }
  }
}

repositories {
  mavenCentral()
  maven { url = 'https://maven.minecraftforge.net' }
  maven { url = 'https://repo.spongepowered.org/maven' }
  flatDir { dirs 'libs' }
}

dependencies {
  minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

  compileOnly "org.spongepowered:mixin:${project.mixin_version}"
  annotationProcessor "org.spongepowered:mixin:${project.mixin_version}:processor"
  compileOnly "javax.annotation:javax.annotation-api:1.3.2"

  def localMods = [
    "ApothicAttributes:1.20.1-1.3.7",
    "Apotheosis:1.20.1-7.4.8",
    "artifacts-forge:9.5.16",
    "create:1.20.1-6.0.8",
    "createpropulsion:0.3.0",
    "create_blaze_burner_fuels:1.0.2-forge-1.20.1",
    "create_connected:1.1.10-mc1.20.1-all",
    "create_tweaked_controllers:1.20.1-1.2.5",
    "create_ultimate_factory:2.2.2-forge-1.20.1",
    "mna-forge:1.20.1-3.1.11-all",
    "mna_attrib_apoth:0.5-1.20.1",
    "relics:1.20.1-0.8.0.11"
  ]

  localMods.each { modCoordinate ->
    compileOnly fg.deobf("localmods:${modCoordinate}")
    runtimeOnly fg.deobf("localmods:${modCoordinate}")
  }
}

mixin {
  add sourceSets.main, "mixins.innofixes.refmap.json"
  config "mixins.innofixes.json"
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.named('jar').configure {
  finalizedBy 'reobfJar'
  manifest {
    attributes(
      'Specification-Title': 'innofixes',
      'Specification-Version': project.mod_version,
      'Implementation-Title': project.mod_id,
      'Implementation-Version': project.mod_version,
      'MixinConfigs': 'mixins.innofixes.json'
    )
  }
}
